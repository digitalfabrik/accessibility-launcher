plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-parcelize'
}

android {
    namespace 'org.tuerantuer.launcher'
    compileSdk 35

    defaultConfig {
        applicationId "org.tuerantuer.launcher"
        minSdk 26
        targetSdk 35
        if (project.hasProperty('VERSION_CODE')) {
            versionCode project.VERSION_CODE.toInteger()
            versionName project.VERSION_NAME
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty('KEYSTORE_PATH')) {
                storeFile file(project.KEYSTORE_PATH)
                storePassword project.KEYSTORE_PASSWORD
                keyAlias project.KEYSTORE_KEY_ALIAS
                keyPassword project.KEYSTORE_KEY_PASSWORD
            } else {
                storeFile file('debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'

                logger.warn("You should provide a keystore using the " +
                        "KEYSTORE_PATH, KEYSTORE_PASSWORD, KEYSTORE_KEY_ALIAS and KEYSTORE_KEY_PASSWORD " +
                        "project properties! Using a keystore for testing!")
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            // Enables code shrinking, obfuscation, and optimization for only your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the Android Gradle plugin.
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // If you add a new dependency, please also list it in our settings' license screen


    implementation "androidx.core:core-ktx:1.13.1"

    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.7"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.7"

    // Jetpack Compose (UI)
    implementation "androidx.activity:activity-compose:1.9.1"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material3:material3:1.2.1"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.22.0-rc"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "com.google.accompanist:accompanist-flowlayout:0.22.0-rc"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "com.google.accompanist:accompanist-drawablepainter:0.30.0"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    // Timber - More painless console logging
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Hilt - Dependency injection library
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Lifecycle (View Models)
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"

    // Android Room - SQLite database library
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // DataStore - Replaces SharedPreferences
    implementation "androidx.datastore:datastore-preferences:1.1.1"

    // Coil - Image loading library
    implementation "io.coil-kt:coil:2.3.0"
    implementation "io.coil-kt:coil-compose:2.3.0"

    // Testing
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}